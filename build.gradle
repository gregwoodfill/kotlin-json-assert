buildscript {
    dependencies {
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.0'
    }
}

plugins {
    id 'java'
    id 'idea'
    id 'maven'
    id 'maven-publish'
    id "org.jetbrains.kotlin.jvm" version "1.1.51"
    id "com.jfrog.bintray" version "1.7.3"
    id "io.gitlab.arturbosch.detekt" version "1.0.0.RC4-3"
}

apply plugin: 'org.junit.platform.gradle.plugin'

group 'com.gregwoodfill.assert'
version '0.1.0'

ext.jsonassert_version = '1.5.+'

task wrapper(type: Wrapper) {
    version = "4.2.1"
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['PublishKotlinJsonAssert']
    configurations = ['archives']
    pkg {
        repo = 'maven'
        name = 'kotlin-json-assert'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/gregwoodfill/kotlin-json-assert.git'
    }
}

junitPlatform {
    filters {
        engines {
            include 'spek'
        }
    }
}

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
    maven { url "http://dl.bintray.com/jetbrains/spek" }
}

dependencies {
    compile "org.skyscreamer:jsonassert:$jsonassert_version"
    compile "org.jetbrains.kotlin:kotlin-stdlib"
    compile "org.jetbrains.kotlin:kotlin-reflect"

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile 'org.amshove.kluent:kluent:1.+'
    testCompile 'org.jetbrains.spek:spek-api:1.1.5'
    testRuntime ('org.jetbrains.spek:spek-junit-platform-engine:1.1.5') {
        exclude group: 'org.junit.platform'
        exclude group: 'org.jetbrains.kotlin'
    }
    testRuntime('org.junit.jupiter:junit-jupiter-api:5.0.1')
    testRuntime('org.junit.jupiter:junit-jupiter-engine:5.0.1')
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

build.dependsOn(sourcesJar)
bintrayUpload.dependsOn install

publishing {
    publications {
        PublishKotlinJsonAssert(MavenPublication) {
            from components.java
            artifact sourcesJar
            groupId 'com.gregwoodfill.assert'
            artifactId 'kotlin-json-assert'
            version project.version

            // http://stackoverflow.com/a/32353697/6556970
            pom.withXml {
                Map resolvedVersionMap = [:]
                Set<ResolvedArtifact> resolvedArtifacts = configurations.compile.getResolvedConfiguration().getResolvedArtifacts()
                resolvedArtifacts.addAll(configurations.testCompile.getResolvedConfiguration().getResolvedArtifacts())
                resolvedArtifacts.each {
                    ModuleVersionIdentifier mvi = it.getModuleVersion().getId();
                    resolvedVersionMap.put("${mvi.getGroup()}:${mvi.getName()}", mvi.getVersion())
                }

                asNode().dependencies.first().each {
                    def groupId = it.get("groupId").first().value().first()
                    def artifactId = it.get("artifactId").first().value().first()
                    it.get("version").first().value = resolvedVersionMap.get("${groupId}:${artifactId}")
                }
            }
        }
    }
}

detekt {
    version = "1.0.0.RC4-3"
    profile("main") {
        input = "$projectDir/src/main/kotlin"
        config = "$projectDir/detekt.yml"
        filters = ".*test.*,.*/resources/.*,.*/tmp/.*"
    }
}

compileJava.dependsOn(detektCheck)